import org.apache.tools.ant.filters.ReplaceTokens
plugins {
  id "com.github.hierynomus.license" version "0.12.1"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'

group 'com.xebialabs.xlplatform.plugin'
version '1.3.2'
sourceCompatibility = 1.7
targetCompatibility = 1.7

def xlReleaseHome = project.hasProperty('xlReleaseHome') ? project.xlReleaseHome : System.env.XLRELEASE_HOME
if (!xlReleaseHome) {
  throw new RuntimeException("Please specify location of unpacked XL Release server installation in your " +
      "gradle.properties or with '-PxlReleaseHome=/path/to/xl/release/server' command line option")
}

dependencies {
  compile fileTree(dir: "$xlReleaseHome/lib", include: ['*.jar'])
}

processResources.configure {
  filter ReplaceTokens, tokens: [
      'project.version': version.toString(),
      'project.name': rootProject.name
  ]
}

license {
  header rootProject.file('src/main/license/xebialabs_community.license')
  strictCheck true
  excludes(["**/*.json", "**/*.conf"])
}
